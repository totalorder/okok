plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'net.researchgate.release' version '2.7.0'
    id 'io.franzbecker.gradle-lombok' version '1.14'
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.11.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
    compile 'com.google.code.findbugs:jsr305:3.0.2'

    testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

lombok {
    version = "1.18.2"
    sha256 = ""
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('SONATYPE_USERNAME')) {
    signing {
        sign configurations.archives
    }
    
    ext."signing.keyId" = SIGNING_KEY_ID
    ext."signing.secretKeyRingFile" = SIGNING_RING_FILE
    ext."signing.password" = SIGNING_PASSWORD

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: SONATYPE_USERNAME, password: SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: SONATYPE_USERNAME, password: SONATYPE_PASSWORD)
                }

                pom.project {
                    name 'TxMan'
                    packaging 'jar'
                    // optionally artifactId can be defined here 
                    description 'Restful wrapper around OkHttp '
                    url 'https://github.com/totalorder/okok'

                    scm {
                        connection 'git@github.com:totalorder/okok.git'
                        developerConnection 'git@github.com:totalorder/okok.git'
                        url 'https://github.com/totalorder/okok'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://github.com/totalorder/okok/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'totalorder'
                            name 'Anton Blomberg'
                            email 'anton@deadlock.se'
                        }
                    }
                }
            }
        }
    }
}